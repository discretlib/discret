/*
    mutation mutationName{


    friend {
    id : $id
    name: "name" //update name
    truc: {id:$idtruc}
    atruc: [{id:$id}, {name:"qdqs"}] //add $idtruc and create name then add it
    }
    }
    }
*/
WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

identifier = @{ (LETTER | NUMBER | "_")+ }
variable   = @{ "$" ~ identifier }

mutation = { SOI ~ "mutation" ~ identifier ~ "{" ~ entity+ ~ "}" ~ EOI }

entity      = { entity_name ~ "{" ~ field+ ~ "}" }
entity_name = { identifier ~ (":" ~ identifier)? }

field = { identifier ~ ":" ~ value }

value = { variable | entity_ref | entity_array | string | float | integer | boolean | null }

entity_ref   = { "{" ~ field+ ~ "}" }
entity_array = { "[" ~ entity_ref ~ ("," ~ entity_ref)* ~ ","? ~ "]" }

string = ${ "\"" ~ inner ~ "\"" }
inner  = @{ char* }
char   =  {
    !("\"" | "\\") ~ ANY
  | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
  | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

float = @{
    "-"? ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*) ~ ("." ~ ASCII_DIGIT*) ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}

integer = @{
    "-"? ~ ASCII_DIGIT+
}
boolean =  { ^"true" | ^"false" }
null    =  { ^"null" }
