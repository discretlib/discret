/*
    mutation mutationName{

    // if id is passed -> update
    // else create
    friend {
    id : $id
    name: "name" //update name
    truc: {$idtruc}
    atruc: [$idtuc, $idtruc2] //add $idtruc and create name
    }
    }
    }
*/
WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

identifier = @{ (LETTER | NUMBER | "_")+ }
variable   = @{ "$" ~ identifier }

mutation = { SOI ~ "mutation" ~ identifier ~ "{" ~ entity+ ~ "}" ~ EOI }

entity = { identifier ~ "{" ~ field+ ~ "}" }

field = { identifier ~ ":" ~ value }

value = { variable | entity_ref | entity_array | string | float | integer | boolean | null }

entity_ref   = { "{" ~ entity_var ~ "}" }
entity_array = { "[" ~ entity_var ~ "]" }
entity_var   = { variable | string }

string = ${ "\"" ~ inner ~ "\"" }
inner  = @{ char* }
char   =  {
    !("\"" | "\\") ~ ANY
  | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
  | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

float = @{
    "-"? ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*) ~ ("." ~ ASCII_DIGIT*) ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}

integer = @{
    "-"? ~ ASCII_DIGIT+
}
boolean =  { ^"true" | ^"false" }
null    =  { ^"null" }
