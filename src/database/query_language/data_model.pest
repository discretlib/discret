WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

identifier = @{ (LETTER | NUMBER | "_")+ }

deprecated = @{ "@deprecated" }

datamodel = { SOI ~ entity* ~ EOI }
entity    = { deprecated? ~ identifier ~ "{" ~ field ~ ("," ~ field)* ~ ","? ~ "}" }

not_null     = { ^"not null" }
indexed      = { ^"indexed" }
unique       = { ^"unique" }
scalar_type  = { "Integer" | "Float" | "Boolean" | "String" }
scalar_field = { scalar_type ~ (not_null | indexed | unique)* }
entity_array = { "[" ~ identifier ~ "]" }
entity_field = { identifier ~ not_null? }

field = { identifier ~ ":" ~ (entity_array | scalar_field | entity_field) }
