WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

identifier = @{ (LETTER | NUMBER | "_")+ }
variable   = @{ "$" ~ identifier }

query      = { SOI ~ query_type ~ identifier ~ "{" ~ entity+ ~ "}" ~ EOI }
query_type = { "query" | "subscription" }

entity      = { entity_name ~ entity_param? ~ "{" ~ field+ ~ "}" }
entity_name = { identifier ~ (":" ~ identifier)? }

named_field = { identifier ~ (":" ~ identifier)? }
field       = { entity | function | named_field }

entity_param = {
    "(" ~ ")"
  | "(" ~ param ~ ("," ~ param)* ~ ","? ~ ")"
}

param = { search | order_by | limit | skip | before | after | filter }

search       = { "search" ~ "(" ~ search_value ~ ")" }
search_value = { variable | string }

order_by        = { "order_by" ~ "(" ~ order_param ~ ("," ~ order_param)* ~ ","? ~ ")" }
order_param     = { identifier ~ order_direction }
order_direction = { ^"asc" | ^"desc" }

limit       = { "limit " ~ limit_value }
skip        = { "skip " ~ limit_value }
limit_value = { unsigned_int | variable }

before       = { "before " ~ before_value }
after        = { "after " ~ before_value }
before_value = { variable | string }

filter       = { identifier ~ (gtOrEq | ltOrEq | eq | gt | lt) ~ filter_value }
filter_value = { variable | float | string | integer | boolean | null }

eq     = { "=" }
gt     = { ">" }
gtOrEq = { ">=" }
lt     = { "<" }
ltOrEq = { "<=" }

string = ${ "\"" ~ inner ~ "\"" }
inner  = @{ char* }
char   =  {
    !("\"" | "\\") ~ ANY
  | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
  | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

float = @{
    "-"? ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*) ~ ("." ~ ASCII_DIGIT*) ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}

integer = @{
    "-"? ~ ASCII_DIGIT+
}
boolean =  { ^"true" | ^"false" }

unsigned_int = { ASCII_DIGIT+ }

null = { ^"null" }

function      = { identifier ~ ":" ~ function_list }
function_list = { avg_fn | count_fn | max_fn | min_fn | sum_fn | ref_by_fn }

ref_by_fn = { "ref_by" ~ "(" ~ identifier ~ "," ~ identifier ~ entity_param? ~ "{" ~ field+ ~ "}" ~ ")" }
avg_fn    = { "avg" ~ "(" ~ identifier ~ ")" }
count_fn  = { "count" ~ "(" ~ ")" }
max_fn    = { "max" ~ "(" ~ identifier ~ ")" }
min_fn    = { "min" ~ "(" ~ identifier ~ ")" }
sum_fn    = { "sum" ~ "(" ~ identifier ~ ")" }
